# Add a long padded rpath, so the installer can replace it with a relative rpath
!cross_compile: QMAKE_RPATHDIR += "$$INSTALL_PREFIX/long_padding/long_padding/long_padding/long_padding/long_padding\
                   long_padding/long_padding/long_padding/long_padding/long_padding/long_padding/"

CONFIG += skip_target_version_ext
TARGET = $$qtLibraryTarget($$TARGET)
android: DESTDIR = $$BUILD_DIR/imports_shared_cpp/shared/$$replace(uri, \\., /)
else: DESTDIR = $$BUILD_DIR/imports_shared/shared/$$replace(uri, \\., /)

# if a qmldir exists automatically add it to the EXTRA_FILES
exists($$_PRO_FILE_PWD_/qmldir) {
    EXTRA_FILES += $$_PRO_FILE_PWD_/qmldir
}

# Always copy all EXTRA_FILES to the plugin destination
for(extra_file, EXTRA_FILES) {
    exists($$extra_file) {
        file = $$extra_file
    } else {
        file = $$_PRO_FILE_PWD_/$${extra_file}
    }
    file = $$replace(file, /, $$QMAKE_DIR_SEP)
    target = $$replace(DESTDIR, /, $$QMAKE_DIR_SEP)$${QMAKE_DIR_SEP}$$basename(file)
    copy_$${extra_file}.target = $$target
    copy_$${extra_file}.depends = $$file
    copy_$${extra_file}.commands = $(COPY_FILE) \"$$file\" \"$$target\"
    QMAKE_EXTRA_TARGETS += copy_$${extra_file}
    PRE_TARGETDEPS += $$target
}

for(extra_file, EXTRA_FILES) {
    extra_files_install.files += $${extra_file}
    DISTFILES += $${extra_file}
}

include(../config.pri)

android: installPath = $$INSTALL_PREFIX/imports_shared_cpp/shared/$$replace(uri, \\., /)
else: installPath = $$INSTALL_PREFIX/neptune3/imports_shared/shared/$$replace(uri, \\., /)
target.path = $$installPath
extra_files_install.path = $$installPath
INSTALLS += target extra_files_install
